//Blackbox to convert dates
import de.jena.publictransport.simulator.sensinact.mmt.util.DateToInstantBlackbox;
import de.jena.publictransport.simulator.sensinact.mmt.util.LocationToSensinactGeoJsonBlackbox;

modeltype XML "strict" uses type('http://www.eclipse.org/emf/2003/XMLType');
modeltype ECORE "strict" uses ecore('http://www.eclipse.org/emf/2002/Ecore');
modeltype SIMULATOR uses "http://jena.de/udp/trafficos/publictransport/1.0";
modeltype SENSINACT uses "https://jena.de/models/ibis/sensinact/1.0";
modeltype PROVIDER uses "https://eclipse.org/sensinact/core/provider/1.0";

transformation ibisSimulatorToSensinact(in simulator : SIMULATOR, out sensinact : SENSINACT);

main() {
	simulator.rootObjects()[SIMULATOR::PublicTransportDataValue] -> map simulatorToService(); 	
}


mapping SIMULATOR::PublicTransportDataValue::simulatorToService(): SENSINACT::IbisDevice {
	
	var value : PublicTransportDataValueObject = self.value;
	var lineId : Integer := self.timeTableEntryRef.oclAsType(Integer) + 601;
	result.id := lineId.toString();
	result.admin := self.map toAdmin(); 
	
	if(value.oclIsTypeOf(SIMULATOR::PublicTransportDoorChange)) {
		if(value.oclAsType(SIMULATOR::PublicTransportDoorChange).doorId = "0") {
			result.door1State := value.oclAsType(SIMULATOR::PublicTransportDoorChange).map toDoorState();
			result.door1State.timestamp := getInstant(self.timestamp);
		} else if(value.oclAsType(SIMULATOR::PublicTransportDoorChange).doorId = "1") {
			result.door2State := value.oclAsType(SIMULATOR::PublicTransportDoorChange).map toDoorState();
			result.door2State.timestamp := getInstant(self.timestamp);
		} else if(value.oclAsType(SIMULATOR::PublicTransportDoorChange).doorId = "2") {
			result.door3State := value.oclAsType(SIMULATOR::PublicTransportDoorChange).map toDoorState();
			result.door3State.timestamp := getInstant(self.timestamp);
		} else if(value.oclAsType(SIMULATOR::PublicTransportDoorChange).doorId = "3") {
			result.door4State := value.oclAsType(SIMULATOR::PublicTransportDoorChange).map toDoorState();
			result.door4State.timestamp := getInstant(self.timestamp);
		}
		
	} else if(value.oclIsTypeOf(SIMULATOR::PublicTransportDoorCount)) {
		if(value.oclAsType(SIMULATOR::PublicTransportDoorCount).doorId = "0") {
			result.door1CountingState := value.oclAsType(SIMULATOR::PublicTransportDoorCount).map toDoorCount();
			result.door1CountingState.timestamp := getInstant(self.timestamp);
		} else if(value.oclAsType(SIMULATOR::PublicTransportDoorCount).doorId = "1") {
			result.door2CountingState := value.oclAsType(SIMULATOR::PublicTransportDoorCount).map toDoorCount();
			result.door2CountingState.timestamp := getInstant(self.timestamp);
		} else if(value.oclAsType(SIMULATOR::PublicTransportDoorCount).doorId = "2") {
			result.door3CountingState := value.oclAsType(SIMULATOR::PublicTransportDoorCount).map toDoorCount();
			result.door3CountingState.timestamp := getInstant(self.timestamp);
		} else if(value.oclAsType(SIMULATOR::PublicTransportDoorCount).doorId = "3") {
			result.door4CountingState := value.oclAsType(SIMULATOR::PublicTransportDoorCount).map toDoorCount();
			result.door4CountingState.timestamp := getInstant(self.timestamp);
		}
		
	} else if(value.oclIsTypeOf(SIMULATOR::PublicTransportStopRequested)) {
		result.stopRequested := value.oclAsType(SIMULATOR::PublicTransportStopRequested).map toStopRequested();
		result.stopRequested.timestamp := getInstant(self.timestamp);		
	};
	
	if(result.door1State.oclIsUndefined()) {
		result.door1State := setAsUnchanged("DoorState").oclAsType(SENSINACT::DoorState);
	};
	if(result.door2State.oclIsUndefined()) {
		result.door2State := setAsUnchanged("DoorState").oclAsType(SENSINACT::DoorState);
	};
	if(result.door3State.oclIsUndefined()) {
		result.door3State := setAsUnchanged("DoorState").oclAsType(SENSINACT::DoorState);
	};
	if(result.door4State.oclIsUndefined()) {
		result.door4State := setAsUnchanged("DoorState").oclAsType(SENSINACT::DoorState);
	};
	if(result.door1CountingState.oclIsUndefined()) {
		result.door1CountingState := setAsUnchanged("DoorCount").oclAsType(SENSINACT::PassengerCountingDoorCountingState);
	};
	if(result.door2CountingState.oclIsUndefined()) {
		result.door2CountingState := setAsUnchanged("DoorCount").oclAsType(SENSINACT::PassengerCountingDoorCountingState);
	};
	if(result.door3CountingState.oclIsUndefined()) {
		result.door3CountingState := setAsUnchanged("DoorCount").oclAsType(SENSINACT::PassengerCountingDoorCountingState);
	};
	if(result.door4CountingState.oclIsUndefined()) {
		result.door4CountingState := setAsUnchanged("DoorCount").oclAsType(SENSINACT::PassengerCountingDoorCountingState);
	};
	if(result.stopRequested.oclIsUndefined()) {
		result.stopRequested := setAsUnchanged("StopRequested").oclAsType(SENSINACT::StopRequested);
	};
	
}

mapping SIMULATOR::PublicTransportDataValue::toAdmin() : PROVIDER::Admin {
	
	var eClass := PROVIDER::Admin.oclAsType(ECORE::EClass);
	friendlyName := "Line 2 - Trip Number " + self.timeTableEntryRef.oclAsType(Integer).toString();
	location := self.toLocation();	
	if(location.oclIsUndefined()){
//	We set a timestamp much older than the current one (1st Jan 2023) when the location is null, so sensinact will not update it!
		metadata += toMetadataTimestamp(getInstant(1672547040000));
	};
}

query SIMULATOR::PublicTransportDataValue::toLocation() : PROVIDER::EGeoJsonObject {
	return switch {
		case(self.value.oclIsTypeOf(SIMULATOR::PublicTransportPosition)) {			
			return getGeoJson(self.value.oclAsType(SIMULATOR::PublicTransportPosition).position.latitude, self.value.oclAsType(PublicTransportPosition).position.longitude);
		}
	};
	return getGeoJson(null, null);
}

mapping SIMULATOR::PublicTransportDoorChange::toDoorState() : SENSINACT::DoorState {
	var eClass := SENSINACT::DoorState.oclAsType(ECORE::EClass);

	serviceName := "DoorStateService";
	serviceOperation := "SpecificDoorState";
	
	metadata += toMetadataEntry(eClass.getEStructuralFeature("serviceName"), "DoorState", null, "true", null);
	metadata += toMetadataEntry(eClass.getEStructuralFeature("serviceOperation"), "DoorState", null, "true", null);
	
	doorId := self.doorId;
	if(self.doorId.oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("doorId"), "DoorState", null, "true", null);
	};
	
	doorState := self.type.toString();
	if(self.type.toString().oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("doorState"), "DoorState", null, "true", null);
	};
	
	exitSide := self.doorSide.toString();
	if(self.doorSide.toString().oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("exitSide"), "DoorState", null, "true", null);
	};
}

helper setAsUnchanged(eClassName : String) : PROVIDER::Service {
	if(eClassName = "DoorState") {
		var eClass := SENSINACT::DoorState.oclAsType(ECORE::EClass);
		var res := object SENSINACT::DoorState {
			serviceName := null;
			serviceOperation := null;
			doorId := null;
			doorState := null;
			exitSide := null;
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "serviceName", "DoorState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "serviceOperation", "DoorState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "doorId", "DoorState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "doorState", "DoorState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "exitSide", "DoorState");
		};
		return res;
		
	} else if(eClassName = "DoorCount") {
		var eClass := SENSINACT::PassengerCountingDoorCountingState.oclAsType(ECORE::EClass);
		var res := object SENSINACT::PassengerCountingDoorCountingState {
			serviceName := null;
			serviceOperation := null;
			doorId := null;
			doorCountingType := null;
			exitSide := null;
			_in := null;
			_out := null;
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "serviceName", "PassengerCountingDoorCountingState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "serviceOperation", "PassengerCountingDoorCountingState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "doorId", "PassengerCountingDoorCountingState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "doorCountingType", "PassengerCountingDoorCountingState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "exitSide", "PassengerCountingDoorCountingState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "in", "PassengerCountingDoorCountingState");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "out", "PassengerCountingDoorCountingState");
		};
		return res;
	} else if(eClassName = "StopRequested") {
		var eClass := SENSINACT::StopRequested.oclAsType(ECORE::EClass);
		var res := object SENSINACT::StopRequested {
			serviceName := null;
			serviceOperation := null;
			stopRequested := null;
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "serviceName", "StopRequested");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "serviceOperation", "StopRequested");
			metadata += toMetadataTimestamp(getInstant(1672547040000), eClass, "stopRequested", "StopRequested");	
		};
		return res;
	};
	return null;
}

mapping SIMULATOR::PublicTransportDoorCount::toDoorCount() : SENSINACT::PassengerCountingDoorCountingState {
	var eClass := SENSINACT::PassengerCountingDoorCountingState.oclAsType(ECORE::EClass);

	serviceName := "PassengerCountingService";
	serviceOperation := "DoorCount";
	
	metadata += toMetadataEntry(eClass.getEStructuralFeature("serviceName"), "PassengerCountingDoorCountingState", null, "true", null);
	metadata += toMetadataEntry(eClass.getEStructuralFeature("serviceOperation"), "PassengerCountingDoorCountingState", null, "true", null);
		
	doorId := self.doorId;
	if(self.doorId.oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("doorId"), "PassengerCountingDoorCountingState", null, "true", null);
	};
	
	doorCountingType := self.type.toString();
	if(self.type.toString().oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("doorCountingType"), "PassengerCountingDoorCountingState", null, "true", null);
	};
	
	exitSide := self.doorSide.toString();
	if(self.doorSide.toString().oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("exitSide"), "PassengerCountingDoorCountingState", null, "true", null);
	};
	
	_in := self._in;
	if(self._in.oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("in"), "PassengerCountingDoorCountingState", null, "true", null);
	};
	
	_out := self._out;
	if(self._out.oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("out"), "PassengerCountingDoorCountingState", null, "true", null);
	};
}

mapping SIMULATOR::PublicTransportStopRequested::toStopRequested() : SENSINACT::StopRequested {
	var eClass := SENSINACT::StopRequested.oclAsType(ECORE::EClass);

	serviceName := "PassengerInfoService";
	serviceOperation := "StopRequested";
	
	metadata += toMetadataEntry(eClass.getEStructuralFeature("serviceName"), "StopRequested", null, "true", null);
	metadata += toMetadataEntry(eClass.getEStructuralFeature("serviceOperation"), "StopRequested", null, "true", null);
		
	stopRequested := self.stopRequested;
	if(self.stopRequested.oclIsUndefined().not()){
		metadata += toMetadataEntry(eClass.getEStructuralFeature("stopRequested"), "StopRequested", null, "true", null);
	};
}

helper toMetadataEntry(feature : ECORE::EStructuralFeature, friendlyServiceName : String, unit : String, hide : String, type : String): PROVIDER::FeatureMetadata {
	var entry := object PROVIDER::FeatureMetadata {
		key := feature;
		value := toMetadata(friendlyServiceName + "-" + feature.name, unit, hide, type); 
	};
	return entry;	
}

helper toMetadata(friendlyNameParam : String, unit : String, hide : String, type : String): PROVIDER::Metadata{
	var res := object PROVIDER::Metadata {
		extra += toCustomMetadata("friendlyName", friendlyNameParam);
		if(unit != null and unit != ""){
			extra += toCustomMetadata("unit", unit);
			extra += toCustomMetadata("sensorthings.unit.name", unit);
		};
		if(hide.oclIsUndefined().not()){
			extra += toCustomMetadata("sensorthings.hide", hide);
		};
		if(type.oclIsUndefined().not()){
			extra += toCustomMetadata("sensorthings.datastream.type", type);
		};
	};	
	return res;
}

helper toMetadataTimestamp(value : EInstant): PROVIDER::FeatureMetadata {
	var entry := toMetadataEntry(PROVIDER::Admin.oclAsType(ECORE::EClass).getEStructuralFeature("location"), "Admin", null, "true", null);
	entry.value.timestamp := value;
	return entry;
}

helper toMetadataTimestamp(value : EInstant, eClass : EClass, featureName : String, friendlyName : String): PROVIDER::FeatureMetadata {
	var entry := toMetadataEntry(eClass.getEStructuralFeature(featureName), friendlyName, null, "true", null);
	entry.value.timestamp := value;
	return entry;
}

helper toCustomMetadata(key : String, theValue : String): PROVIDER::FeatureCustomMetadata{
	var entry := object PROVIDER::FeatureCustomMetadata{
		name := key;
		value := theValue;
	};
	return entry;
}
